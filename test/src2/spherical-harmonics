"Spherical harmonics"

clear

Y(l,m) = (-1)^m sqrt((2 l + 1) / (4 pi) (l - m)! / (l + m)!) *
         P(l,m) exp(i m phi)

-- associated legendre of cos theta

P(l,m) = test(m < 0, (-1)^(-m) (l + m)! / (l - m)! P(l,-m),
         1 / (2^l l!) sin(theta)^m *
         eval(d((x^2 - 1)^l,x,l + m),x,cos(theta)))

Lap(f) = 1 / sin(theta) d(sin(theta) d(f,theta),theta) +
         1 / sin(theta)^2 d(f,phi,2)

for(l,0,3,for(m,-l,l,check(-Lap(Y(l,m)) == l (l + 1) Y(l,m))))

check(Y(1,1) == -sqrt(3 / 8 / pi) sin(theta) exp(i phi))
check(Y(1,0) == sqrt(3 / 4 / pi) cos(theta))
check(Y(2,2) == 1/4 sqrt(15 / 2 / pi) sin(theta)^2 exp(2 i phi))
check(Y(2,1) == -sqrt(15 / 8 / pi) sin(theta) cos(theta) exp(i phi))
check(Y(2,0) == 1/2 sqrt(5 / 4 / pi) (3 cos(theta)^2 - 1))

check(Y(1,-1) == -conj(Y(1,1)))
check(Y(2,-2) == conj(Y(2,2)))
check(Y(2,-1) == -conj(Y(2,1)))
