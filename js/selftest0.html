<html>
<body>

<script src="eigenmath.js"></script>

<table><tr><td>
<textarea id="stdin" rows="24" cols="80" style="font-family:courier;font-size:12pt">
check(abs(-1.0) == 1.0)
check(adj(((a,b),(c,d))) == ((d,-b),(-c,a)))
check(adj(((a11,a12,a13),(a21,a22,a23),(a31,a32,a33))) == ((a22 a33 - a23 a32,-a12 a33 + a13 a32,a12 a23 - a13 a22),(-a21 a33 + a23 a31,a11 a33 - a13 a31,-a11 a23 + a13 a21),(a21 a32 - a22 a31,-a11 a32 + a12 a31,a11 a22 - a12 a21)))
check(and(1,2) == 1)
check(arccos(1.0) == 0.0)
check(arccosh(1.0) == 0.0)
check(arcsin(0.0) == 0.0)
check(arcsinh(0.0) == 0.0)
check(arctan(0.0) == 0.0)
check(arctanh(0.0) == 0.0)
check(arg(0.0) == 0.0)
check(ceiling(1.2) == 2)
check(circexp(sin(x)) == -1/2 i exp(i x) + 1/2 i exp(-i x))
check(prefixform(clock(1-i)) == "(* (^ 2 1/2) (^ -1 -1/4))")
check(conj(1 - i) == 1 + i)
check(contract(((a,b),(c,d))) == a + d)
check(cos(0.0) == 1.0)
check(cosh(0.0) == 1.0)
check(defint(x,x,1,2) == 3/2)
check(denominator(a/b) == b)
check(derivative(x,x) == 1)
check(det(((a,b),(c,d))) == a d - b c)
check(do(a,b,c) == c)
check(dot((a,b),(c,d)) == a c + b d)
check(eval(2 x,x,3) == 6)
check(exp(0.0) == 1.0)
check(expcos(x) == 1/2 exp(i x) + 1/2 exp(-i x))
check(expcosh(x) == 1/2 exp(-x) + 1/2 exp(x))
check(expsin(x) == -1/2 i exp(i x) + 1/2 i exp(-i x))
check(expsinh(x) == -1/2 exp(-x) + 1/2 exp(x))
check(exptan(x) == i / (exp(2 i x) + 1) - i exp(2 i x) / (exp(2 i x) + 1))
check(exptanh(x) == -1 / (exp(2 x) + 1) + exp(2 x) / (exp(2 x) + 1))
check(factorial(6) == 720)
check(prefixform(float(1/2)) == "0.500000")
check(floor(3/2) == 1)
check(for(k,1,3,k) == nil)
check(imag(2 - 3 i) == -3)
check(inner((a,b),(c,d)) == a c + b d)
check(integral(x,x) == 1/2 x^2)
check(inv(((a,b),(c,d))) == ((d / (a d - b c),-b / (a d - b c)),(-c / (a d - b c),a / (a d - b c))))
check(log(1.0) == 0.0)
check(mag(2 - 3 i) == 13^(1/2))
check(not(0) == 1)
check(number(1) == 1)
check(numerator(a/b) == a)
check(or(0,0) == 0)
check(outer((a,b),(c,d)) == ((a c,a d),(b c,b d)))
check(prefixform(polar(2 - 3 i)) == "(* (^ 13 1/2) (^ $e (* (^ -1 1/2) (arctan -3 2))))")
check(product(k,1,4,k) == 24)
check(prefixform(quote(1 + 2)) == "(+ 1 2)")
check(rank(outer((1,2),(3,4),(5,6))) == 3)
check(prefixform(rationalize(1/a + 1/b)) == "(* (^ a -1) (^ b -1) (+ a b))")
check(real(2 - 3 i) == 2)
check(rect((-1)^(1/8)) == cos(1/8 pi) + i sin(1/8 pi))
check(sgn(-2) == -1)
check(sin(0) == 0)
check(sinh(0) == 0)
check(prefixform(sqrt(2)) == "(^ 2 1/2)")
check(sum(k,1,4,k) == 10)
check(tan(0) == 0)
check(tanh(0) == 0)
check(test(a == b,1,0) == 0)
check(transpose(((a,b),(c,d))) == ((a,c),(b,d)))
check(unit(2) == ((1,0),(0,1)))
check(zero(2,3) == ((0,0,0),(0,0,0)))

check(prefixform(a-b)="(+ a (* -1 b))")
check(prefixform(b-a)="(+ (* -1 a) b)")

"Testing complex number arithmetic"

A = (-1)^(1/8)
check(prefixform(A)="(^ -1 1/8)")
A = (-1)^(3/8)
check(prefixform(A)="(^ -1 3/8)")
A = (-1)^(5/8)
check(prefixform(A)="(* -1 (^ -1 -3/8))")
A = (-1)^(7/8)
check(prefixform(A)="(* -1 (^ -1 -1/8))")
A = (-1)^(9/8)
check(prefixform(A)="(* -1 (^ -1 1/8))")
A = (-1)^(11/8)
check(prefixform(A)="(* -1 (^ -1 3/8))")
A = (-1)^(13/8)
check(prefixform(A)="(^ -1 -3/8)")
A = (-1)^(15/8)
check(prefixform(A)="(^ -1 -1/8)")

A = (-1)^(-1/8)
check(prefixform(A)="(^ -1 -1/8)")
A = (-1)^(-3/8)
check(prefixform(A)="(^ -1 -3/8)")
A = (-1)^(-5/8)
check(prefixform(A)="(* -1 (^ -1 3/8))")
A = (-1)^(-7/8)
check(prefixform(A)="(* -1 (^ -1 1/8))")
A = (-1)^(-9/8)
check(prefixform(A)="(* -1 (^ -1 -1/8))")
A = (-1)^(-11/8)
check(prefixform(A)="(* -1 (^ -1 -3/8))")
A = (-1)^(-13/8)
check(prefixform(A)="(^ -1 3/8)")
A = (-1)^(-15/8)
check(prefixform(A)="(^ -1 1/8)")

A = (1+i)^2
check(prefixform(A)="(* 2 (^ -1 1/2))")

A = (2*i)^2
check(A=-4)

A = (2+3*i)^2
check(prefixform(A)="(+ -5 (* 12 (^ -1 1/2)))")

A = (2+3*i)^(-2)
check(prefixform(A)"(+ -5/169 (* -12/169 (^ -1 1/2)))")

A = i^0
check(A=1)

A = i^1
check(prefixform(A)="(^ -1 1/2)")

A = i^2
check(A=-1)

A = i^3
check(prefixform(A)="(* -1 (^ -1 1/2))")

A = i^4
check(A=1)

A = i^5
check(prefixform(A)="(^ -1 1/2)")

A = i^6
check(A=-1)

A = i^7
check(prefixform(A)="(* -1 (^ -1 1/2))")

A = i^(-1)
check(prefixform(A)="(* -1 (^ -1 1/2))")

A = i^(-2)
check(A=-1)

A = i^(-3)
check(prefixform(A)="(^ -1 1/2)")

A = i^(-4)
check(A=1)

A = i^(-5)
check(prefixform(A)="(* -1 (^ -1 1/2))")

A = i^(-6)
check(A=-1)

A = i^(-7)
check(prefixform(A)="(^ -1 1/2)")

for(k,1,100,
N=2^32-k,
M=sqrt(N),
check(N/M=M))

"Testing inv"
clear

A = ((A11,A12),(A21,A22))
B = dot(A,inv(A))
check(B=unit(2))

A = ((A11,A12,A13),(A21,A22,A23),(A31,A32,A33))
B = dot(A,inv(A))
check(B=unit(3))

A = ((A11,A12,A13,A14),(A21,A22,A23,A24),(A31,A32,A33,A34),(A41,A42,A43,A44))
B = dot(A,inv(A))
check(B=unit(4))

"Testing transpose"
clear

A = (A1,A2,A3)
B = (B1,B2,B3,B4)
C = (C1,C2,C3,C4,C5)
D = (D1,D2,D3,D4,D5)
E = (E1,E2,E3,E4,E5,E6)
F = (F1,F2,F3,F4,F5,F6,F7)

G = dot(outer(A,B,C),outer(D,E,F))
H = contract(outer(A,B,C,D,E,F),3,4)
check(G=H)

G = transpose(outer(A,B,C,D,E,F))
H = outer(B,A,C,D,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),1,2)
H = outer(B,A,C,D,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),1,3)
H = outer(C,B,A,D,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),1,4)
H = outer(D,B,C,A,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),1,5)
H = outer(E,B,C,D,A,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),1,6)
H = outer(F,B,C,D,E,A)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),2,3)
H = outer(A,C,B,D,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),2,4)
H = outer(A,D,C,B,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),2,5)
H = outer(A,E,C,D,B,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),2,6)
H = outer(A,F,C,D,E,B)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),3,4)
H = outer(A,B,D,C,E,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),3,5)
H = outer(A,B,E,D,C,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),3,6)
H = outer(A,B,F,D,E,C)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),4,5)
H = outer(A,B,C,E,D,F)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),4,6)
H = outer(A,B,C,F,E,D)
check(G=H)

G = transpose(outer(A,B,C,D,E,F),5,6)
H = outer(A,B,C,D,F,E)
check(G=H)

"Testing dot"

A = (A1,A2,A3)
B = (B1,B2,B3)
check(contract(outer(A,B))=dot(A,B))

A = (A1,A2,A3,A4)
B = (B1,B2,B3)
C = (C1,C2,C3)
check(contract(outer(A,B,C),2,3)=dot(B,C)*A)

A = (A1,A2,A3)
B = (B1,B2,B3)
C = (C1,C2,C3,C4)
check(contract(outer(A,B,C))=dot(A,B)*C)

clear

T = -3*x*A/(A-B) + 3*x*B/(A-B)
check(T=-3*x)

A = -y/(x^2*(y^2/x^2+1))
check(A=-y/(x^2+y^2))

A = 1/(x*(y^2/x^2+1))
check(A=x/(x^2+y^2))

A = -i * exp(i * x) / (2 * (1/2 * exp(i * x) + 1/2 * exp(-i * x)))
check(A=-i * exp(2 * i * x) / (exp(2 * i * x) + 1))

A = i * exp(-i * x) / (2 * (1/2 * exp(i * x) + 1/2 * exp(-i * x)))
check(A=i / (exp(2 * i * x) + 1))

A = numerator(-(-i + i * exp(2 * i * x)) / (exp(2 * i * x) + 1))
check(A=i - i * exp(2 * i * x))
A = denominator(-(-i + i * exp(2 * i * x)) / (exp(2 * i * x) + 1))
check(A=exp(2 * i * x) + 1)

check(exptan(z)=(exp(2*i*z)-1)/(i*(exp(2*i*z)+1)))

"Testing arctan"

check(arctan(1,1)=pi/4)
check(arctan(1,-1)=3*pi/4)
check(arctan(-1,1)=-pi/4)
check(arctan(-1,-1)=-3*pi/4)

check(arctan(0,3)=0)
check(arctan(0,-3)=-pi)
check(arctan(3,0)=pi/2)
check(arctan(-3,0)=-pi/2)

check(arctan(3,3)=pi/4)
check(arctan(3,-3)=3*pi/4)
check(arctan(-3,3)=-pi/4)
check(arctan(-3,-3)=-3*pi/4)

-- complex numbers

z1 = 1+i     -- z1 does not have arctan in polar form
z2 = 2+3*i   -- z2 does have arctan in polar form

check(arg(z1)=pi/4)
check(mag(z1)=sqrt(2))

check(arg(z2)=arctan(3,2))
check(mag(z2)=sqrt(13))

check(arg(polar(z1))=pi/4)
check(arg(clock(z1))=pi/4)

check(arg(polar(z2))=arctan(3,2))
check(arg(clock(z2))=arctan(3,2))

check(mag(polar(z1))=sqrt(2))
check(mag(clock(z1))=sqrt(2))

check(mag(polar(z2))=sqrt(13))
check(mag(clock(z2))=sqrt(13))

check(z1*z2=rect(clock(z1)*clock(z2)))
check(z1*z2=rect(clock(z1)*polar(z2)))
check(z1*z2=rect(polar(z1)*clock(z2)))

A = prefixform(arccosh(x))
check(A="(arccosh x)")

A = prefixform(arccosh(cosh(x)))
check(A="x")

A = prefixform(arccosh(0))
check(A="(arccosh 0)")

A = prefixform(float(exp(x)))
check(A="(^ $e x)")

A = prefixform(exp(0))
check(A="1")

A = prefixform(exp(0.0))
check(A="1.00000")

A = prefixform(exp(1))
check(A="$e")

A = prefixform(exp(1.0))
check(A="2.71828")

A = prefixform(exp(1.0))
check(A="2.71828")

A = prefixform(exp(2.0))
check(A="7.38906")

A = prefixform(exp(2.0))
check(A="7.38906")

A = prefixform(float(exp(1)))
check(A="2.71828")

A = prefixform(float(exp(i)))
check(A="(+ 0.540302 (* 0.841471 (^ -1 1/2)))")

A = prefixform(float(exp(-i)))
check(A="(+ 0.540302 (* -0.841471 (^ -1 1/2)))")

A = prefixform(float(exp(-2 + 3 i)))
check(A="(+ -0.133981 (* 0.0190985 (^ -1 1/2)))")

A = prefixform(0^0)
check(A="1")

A = prefixform(0^0.0)
check(A="1.00000")

A = prefixform(0.0^0)
check(A="1.00000")

A = prefixform(0.0^0.0)
check(A="1.00000")

A = prefixform(sqrt(-1.0))
check(A="(* 1.00000 (^ -1 1/2))")

A = prefixform((-1.0)^(1/3))
check(A="(* 1.00000 (^ -1 1/3))")

A = prefixform((-1.0)^(-1/3))
check(A="(* 1.00000 (^ -1 -1/3))")

clear

s = 4 E^2
t = -2 E^2 + E^2 exp(i theta) + E^2 exp(-i theta)
u = -2 E^2 - E^2 exp(i theta) - E^2 exp(-i theta)

A = (s^2 + u^2)/t^2
B = 2 s^2/(t u)
C = (s^2 + t^2)/u^2

X = (1 + expcos(theta/2)^4) / expsin(theta/2)^4
Y = 8 / expsin(theta)^2
Z = (1 + expsin(theta/2)^4) / expcos(theta/2)^4

check(rationalize(A + B + C - X - Y - Z) == 0)

clear

psi = exp(i k z - i omega t)
A = prefixform(1/psi)
check(A == "(^ $e (+ (* -1 (^ -1 1/2) k z) (* (^ -1 1/2) omega t)))")
A = psi^(-1)
check(prefixform(A)=="(^ $e (+ (* -1 (^ -1 1/2) k z) (* (^ -1 1/2) omega t)))")
A = psi/psi
check(prefixform(A)=="1")

-- loop indices are not rewritten

A = zero(3)
f(x) = for(i,1,3,A[i] = x)
f(1)
check(A==(1,1,1))

clear

z = sqrt(i)
A = z conj(z)
check(A == 1)

z = (-1)^a
A = z conj(z)
check(A == 1)

-- tensor comparison

A = (0,0) == (0,0,0) -- both are equal to scalar zero
check(A = 1)

A = (1,0) == (1,0,0) -- should not cause a stop like (1,0) - (1,0,0) does
check(A = 0)

-- tensor promotion

a = quote(a)
b = quote(b)
c = quote(c)
d = quote(d)

A = ((a,b),(c,d))

a = (11,12,13)
b = (21,22,23)
c = (31,32,33)
d = (41,42,43)

B = A

check(B[1,1] = a)
check(B[1,2] = b)
check(B[2,1] = c)
check(B[2,2] = d)

-- verify that defint works in a denominator

clear

I(y) = defint((1 + cos(theta)^2) sin(theta),theta,0,y)
A = I(theta) / I(pi)
check(A = -1/8 cos(theta)^3 - 3/8 cos(theta) + 1/2)

-- log of number is factored

check(log(1/10) = -log(2) - log(5))

-- simplify polar

check(exp(-12/2 i pi) == 1)
check(exp(-11/2 i pi) == i)
check(exp(-10/2 i pi) == -1)
check(exp(-9/2 i pi) == -i)

check(exp(-8/2 i pi) == 1)
check(exp(-7/2 i pi) == i)
check(exp(-6/2 i pi) == -1)
check(exp(-5/2 i pi) == -i)

check(exp(-4/2 i pi) == 1)
check(exp(-3/2 i pi) == i)
check(exp(-2/2 i pi) == -1)
check(exp(-1/2 i pi) == -i)

check(exp(0/2 i pi) == 1)
check(exp(1/2 i pi) == i)
check(exp(2/2 i pi) == -1)
check(exp(3/2 i pi) == -i)

check(exp(4/2 i pi) == 1)
check(exp(5/2 i pi) == i)
check(exp(6/2 i pi) == -1)
check(exp(7/2 i pi) == -i)

check(exp(8/2 i pi) == 1)
check(exp(9/2 i pi) == i)
check(exp(10/2 i pi) == -1)
check(exp(11/2 i pi) == -i)

check(exp(-12/2.0 i pi) == 1)
check(exp(-11/2.0 i pi) == i)
check(exp(-10/2.0 i pi) == -1)
check(exp(-9/2.0 i pi) == -i)

check(exp(-8/2.0 i pi) == 1)
check(exp(-7/2.0 i pi) == i)
check(exp(-6/2.0 i pi) == -1)
check(exp(-5/2.0 i pi) == -i)

check(exp(-4/2.0 i pi) == 1)
check(exp(-3/2.0 i pi) == i)
check(exp(-2/2.0 i pi) == -1)
check(exp(-1/2.0 i pi) == -i)

check(exp(0/2.0 i pi) == 1)
check(exp(1/2.0 i pi) == i)
check(exp(2/2.0 i pi) == -1)
check(exp(3/2.0 i pi) == -i)

check(exp(4/2.0 i pi) == 1)
check(exp(5/2.0 i pi) == i)
check(exp(6/2.0 i pi) == -1)
check(exp(7/2.0 i pi) == -i)

check(exp(8/2.0 i pi) == 1)
check(exp(9/2.0 i pi) == i)
check(exp(10/2.0 i pi) == -1)
check(exp(11/2.0 i pi) == -i)

-- cross product test for equality

clear

P(x,l,m) = 1 / (2^l l!) (1 - x^2)^(m / 2) d((x^2 - 1)^l,x,l + m)

check(P(x,0,0) == 1)
check(P(x,1,0) == x)
check(P(x,1,1) == sqrt(1 - x^2))
check(P(x,1,-1) == -1/2 sqrt(1 - x^2))
check(P(x,2,0) == 1/2 (3 x^2 - 1))
check(P(x,2,1) == 3 x sqrt(1 - x^2))
check(P(x,2,-1) == -1/2 x sqrt(1 - x^2))
check(P(x,2,2) == 3 (1 - x^2))
check(P(x,2,-2) == 1/8 (1 - x^2))

-- tensor equality test

clear

A = ((0,0,0,0),(0,2 M / (r^3 (-2 M / r + 1)) + r^(-2) - 1 / (r^2 (-2 M / r + 1)),0,0),(0,0,0,0),(0,0,0,0))
check(A == 0)
check(0 == A)
check(A == zero(4,4))
check(zero(4,4) == A)
B = A
check(A == B)
B[1,1] = 1
check(not(A == B))

-- set component test
clear
I = ((1,1,1,1),(1,1,1,1))
A = (I,I,I)
A[1] = a I
A[2] = b I
A[3] = c I
check(A == (((a,a,a,a),(a,a,a,a)),((b,b,b,b),(b,b,b,b)),((c,c,c,c),(c,c,c,c))))

"Testing simplify"

clear

A = simplify(-3*x*A/(A-B) + 3*x*B/(A-B))
check(prefixform(A) = "(* -3 x)")

A = simplify(-y/(x^2*(y^2/x^2+1)))
check(prefixform(A) = "(* -1 y (^ (+ (^ x 2) (^ y 2)) -1))")

A = simplify(1/(x*(y^2/x^2+1)))
check(prefixform(A) = "(* x (^ (+ (^ x 2) (^ y 2)) -1))")

A = simplify(-i * exp(i * x) / (2 * (1/2 * exp(i * x) + 1/2 * exp(-i * x))))
check(prefixform(A) = "(* -1 (^ -1 1/2) (^ (+ (^ $e (* 2 (^ -1 1/2) x)) 1) -1) (^ $e (* 2 (^ -1 1/2) x)))")

A = simplify(i * exp(-i * x) / (2 * (1/2 * exp(i * x) + 1/2 * exp(-i * x))))
check(prefixform(A) = "(* (^ -1 1/2) (^ (+ (^ $e (* 2 (^ -1 1/2) x)) 1) -1))")

A = simplify(tan(x) - sin(x)/cos(x))
check(prefixform(A) = "0")

A = simplify(sin(x)^2 + cos(x)^2)
check(prefixform(A) = "1")

A = simplify(sinh(x)^2 - cosh(x)^2)
check(prefixform(A) = "-1")

A = simplify(((sin(x)^2 + cos(x)^2,0),(0,sin(x))))
check(A = ((1,0),(0,sin(x))))

A = simplify(sqrt(1 + x^2 / (1 - x^2)))
check(A == 1 / sqrt(1 - x^2))

"Testing det"

clear
A = ((A11,A12),(A21,A22))
B = A11 A22 - A12 A21
check(det(A) == B)
A = ((A11,A12,A13),(A21,A22,A23),(A31,A32,A33))
B = A11 A22 A33 - A11 A23 A32 - A12 A21 A33 + A12 A23 A31 + A13 A21 A32 - A13 A22 A31
check(det(A) == B)
A = ((A11,A12,A13,A14),(A21,A22,A23,A24),(A31,A32,A33,A34),(A41,A42,A43,A44))
B = A11 A22 A33 A44 -
    A11 A22 A34 A43 -
    A11 A23 A32 A44 +
    A11 A23 A34 A42 +
    A11 A24 A32 A43 -
    A11 A24 A33 A42 -
    A12 A21 A33 A44 +
    A12 A21 A34 A43 +
    A12 A23 A31 A44 -
    A12 A23 A34 A41 -
    A12 A24 A31 A43 +
    A12 A24 A33 A41 +
    A13 A21 A32 A44 -
    A13 A21 A34 A42 -
    A13 A22 A31 A44 +
    A13 A22 A34 A41 +
    A13 A24 A31 A42 -
    A13 A24 A32 A41 -
    A14 A21 A32 A43 +
    A14 A21 A33 A42 +
    A14 A22 A31 A43 -
    A14 A22 A33 A41 -
    A14 A23 A31 A42 +
    A14 A23 A32 A41
check(det(A) == B)
A = ((A11,A12,A13,A14,A15),
     (A21,A22,A23,A24,A25),
     (A31,A32,A33,A34,A35),
     (A41,A42,A43,A44,A45),
     (A51,A52,A53,A54,A55))
B = A11 A22 A33 A44 A55 - A11 A22 A33 A45 A54 - A11 A22 A34 A43 A55 + A11 A22 A34 A45 A53 + A11 A22 A35 A43 A54 -
    A11 A22 A35 A44 A53 - A11 A23 A32 A44 A55 + A11 A23 A32 A45 A54 + A11 A23 A34 A42 A55 - A11 A23 A34 A45 A52 -
    A11 A23 A35 A42 A54 + A11 A23 A35 A44 A52 + A11 A24 A32 A43 A55 - A11 A24 A32 A45 A53 - A11 A24 A33 A42 A55 +
    A11 A24 A33 A45 A52 + A11 A24 A35 A42 A53 - A11 A24 A35 A43 A52 - A11 A25 A32 A43 A54 + A11 A25 A32 A44 A53 +
    A11 A25 A33 A42 A54 - A11 A25 A33 A44 A52 - A11 A25 A34 A42 A53 + A11 A25 A34 A43 A52 - A12 A21 A33 A44 A55 +
    A12 A21 A33 A45 A54 + A12 A21 A34 A43 A55 - A12 A21 A34 A45 A53 - A12 A21 A35 A43 A54 + A12 A21 A35 A44 A53 +
    A12 A23 A31 A44 A55 - A12 A23 A31 A45 A54 - A12 A23 A34 A41 A55 + A12 A23 A34 A45 A51 + A12 A23 A35 A41 A54 -
    A12 A23 A35 A44 A51 - A12 A24 A31 A43 A55 + A12 A24 A31 A45 A53 + A12 A24 A33 A41 A55 - A12 A24 A33 A45 A51 -
    A12 A24 A35 A41 A53 + A12 A24 A35 A43 A51 + A12 A25 A31 A43 A54 - A12 A25 A31 A44 A53 - A12 A25 A33 A41 A54 +
    A12 A25 A33 A44 A51 + A12 A25 A34 A41 A53 - A12 A25 A34 A43 A51 + A13 A21 A32 A44 A55 - A13 A21 A32 A45 A54 -
    A13 A21 A34 A42 A55 + A13 A21 A34 A45 A52 + A13 A21 A35 A42 A54 - A13 A21 A35 A44 A52 - A13 A22 A31 A44 A55 +
    A13 A22 A31 A45 A54 + A13 A22 A34 A41 A55 - A13 A22 A34 A45 A51 - A13 A22 A35 A41 A54 + A13 A22 A35 A44 A51 +
    A13 A24 A31 A42 A55 - A13 A24 A31 A45 A52 - A13 A24 A32 A41 A55 + A13 A24 A32 A45 A51 + A13 A24 A35 A41 A52 -
    A13 A24 A35 A42 A51 - A13 A25 A31 A42 A54 + A13 A25 A31 A44 A52 + A13 A25 A32 A41 A54 - A13 A25 A32 A44 A51 -
    A13 A25 A34 A41 A52 + A13 A25 A34 A42 A51 - A14 A21 A32 A43 A55 + A14 A21 A32 A45 A53 + A14 A21 A33 A42 A55 -
    A14 A21 A33 A45 A52 - A14 A21 A35 A42 A53 + A14 A21 A35 A43 A52 + A14 A22 A31 A43 A55 - A14 A22 A31 A45 A53 -
    A14 A22 A33 A41 A55 + A14 A22 A33 A45 A51 + A14 A22 A35 A41 A53 - A14 A22 A35 A43 A51 - A14 A23 A31 A42 A55 +
    A14 A23 A31 A45 A52 + A14 A23 A32 A41 A55 - A14 A23 A32 A45 A51 - A14 A23 A35 A41 A52 + A14 A23 A35 A42 A51 +
    A14 A25 A31 A42 A53 - A14 A25 A31 A43 A52 - A14 A25 A32 A41 A53 + A14 A25 A32 A43 A51 + A14 A25 A33 A41 A52 -
    A14 A25 A33 A42 A51 + A15 A21 A32 A43 A54 - A15 A21 A32 A44 A53 - A15 A21 A33 A42 A54 + A15 A21 A33 A44 A52 +
    A15 A21 A34 A42 A53 - A15 A21 A34 A43 A52 - A15 A22 A31 A43 A54 + A15 A22 A31 A44 A53 + A15 A22 A33 A41 A54 -
    A15 A22 A33 A44 A51 - A15 A22 A34 A41 A53 + A15 A22 A34 A43 A51 + A15 A23 A31 A42 A54 - A15 A23 A31 A44 A52 -
    A15 A23 A32 A41 A54 + A15 A23 A32 A44 A51 + A15 A23 A34 A41 A52 - A15 A23 A34 A42 A51 - A15 A24 A31 A42 A53 +
    A15 A24 A31 A43 A52 + A15 A24 A32 A41 A53 - A15 A24 A32 A43 A51 - A15 A24 A33 A41 A52 + A15 A24 A33 A42 A51
check(det(A) == B)

"Testing minor"

clear
A = ((A11,A12),(A21,A22))
check(minor(A,1,1) == A22)
check(minor(A,1,2) == A21)
check(minor(A,2,1) == A12)
check(minor(A,2,2) == A11)
A = ((A11,A12,A13),(A21,A22,A23),(A31,A32,A33))
check(minor(A,1,1) == A22 A33 - A23 A32)
check(minor(A,1,2) == A21 A33 - A23 A31)
check(minor(A,1,3) == A21 A32 - A22 A31)
check(minor(A,2,1) == A12 A33 - A13 A32)
check(minor(A,2,2) == A11 A33 - A13 A31)
check(minor(A,2,3) == A11 A32 - A12 A31)
check(minor(A,3,1) == A12 A23 - A13 A22)
check(minor(A,3,2) == A11 A23 - A13 A21)
check(minor(A,3,3) == A11 A22 - A12 A21)

"Testing minormatrix"

A = ((A11,A12),
     (A21,A22))

for(i,1,2,for(j,1,2,check(det(minormatrix(A,i,j)) == minor(A,i,j))))

A = ((A11,A12,A13),
     (A21,A22,A23),
     (A31,A32,A33))

for(i,1,3,for(j,1,3,check(det(minormatrix(A,i,j)) == minor(A,i,j))))

A = ((A11,A12,A13,A14),
     (A21,A22,A23,A24),
     (A31,A32,A33,A34),
     (A41,A42,A43,A44))

for(i,1,4,for(j,1,4,check(det(minormatrix(A,i,j)) == minor(A,i,j))))

A = ((A11,A12,A13,A14,A15),
     (A21,A22,A23,A24,A25),
     (A31,A32,A33,A34,A35),
     (A41,A42,A43,A44,A45),
     (A51,A52,A53,A54,A55))

for(i,1,5,for(j,1,5,check(det(minormatrix(A,i,j)) == minor(A,i,j))))

A = ((A11,A12,A13,A14,A15,A16),
     (A21,A22,A23,A24,A25,A26),
     (A31,A32,A33,A34,A35,A36),
     (A41,A42,A43,A44,A45,A46),
     (A51,A52,A53,A54,A55,A56),
     (A61,A62,A63,A64,A65,A66))

for(i,1,6,for(j,1,6,check(det(minormatrix(A,i,j)) == minor(A,i,j))))

"Testing cofactor"

A = ((A11,A12),(A21,A22))
B = ((0,0),(0,0))
for(i,1,2,for(j,1,2,B[j,i] = cofactor(A,i,j)))
check(adj(A) == B)

A = ((A11,A12,A13),(A21,A22,A23),(A31,A32,A33))
B = ((0,0,0),(0,0,0),(0,0,0))
for(i,1,3,for(j,1,3,B[j,i] = cofactor(A,i,j)))
check(adj(A) == B)

"Testing hadamard"

X = (a,b,c)
check(hadamard(X,X) == (a^2,b^2,c^2))

"Testing kronecker"

A = (2,3)
B = (a,b,c)
check(kronecker(A,B) == (2a,2b,2c,3a,3b,3c))
check(kronecker(B,A) == (2a,3a,2b,3b,2c,3c))

A = (a,b)
B = ((2,3,4),(5,6,7),(8,9,10),(11,12,13))

C = ((2a,3a,4a),
     (5a,6a,7a),
     (8a,9a,10a),
     (11a,12a,13a),
     (2b,3b,4b),
     (5b,6b,7b),
     (8b,9b,10b),
     (11b,12b,13b))

check(kronecker(A,B) == C)

C = ((2a,3a,4a),
     (2b,3b,4b),
     (5a,6a,7a),
     (5b,6b,7b),
     (8a,9a,10a),
     (8b,9b,10b),
     (11a,12a,13a),
     (11b,12b,13b))

check(kronecker(B,A) == C)

A = ((2,3),(4,5))
B = ((a,b),(c,d))

C = ((2a,2b,3a,3b),
     (2c,2d,3c,3d),
     (4a,4b,5a,5b),
     (4c,4d,5c,5d))

check(kronecker(A,B) == C)

"Testing noexpand"

A = ((a^2 - lambda,0,0),(0,b^2 - lambda,0),(0,0,c^2 - lambda))
B = ((1 / (a^2 - lambda),0,0),(0,1 / (b^2 - lambda),0),(0,0,1 / (c^2 - lambda)))
check(noexpand(inv(A)) == B)
check(noexpand(adj(A)/det(A)) == B)

"Testing local variables"

clear
x = 123 -- global x in use, need local x in P
P(f,n,m,x) = eval(1/(2^n n!) (1 - x^2)^(m/2) d((x^2 - 1)^n,x,n + m),x,f)
check(P(cos(theta),2,0) == 3/2 cos(theta)^2 - 1/2)

"Testing abs"

clear
check(abs(7) == 7)
check(abs(-7) == 7)
check(prefixform(abs(x)) == "(abs x)")
check(prefixform(abs(-x)) == "(abs x)")
check(abs(3 - 4 i) == 5)
check(abs(r exp(i theta)) == abs(r))
check(abs(cos(5/8 pi)) == -cos(5/8 pi))
check(abs(x y) == abs(x) abs(y))
check(abs(1/x) == 1/abs(x))
check(prefixform(abs(b - a)) == "(abs (+ a (* -1 b)))")

t = abs(2)
check(t = 2)
t = abs(2.0)
check(t = 2)
t = abs(-2)
check(t = 2)
t = abs(-2.0)
check(t = 2)
t = abs(a)
check(t = abs(a))
t = abs(-a)
check(t = abs(a))
t = abs(2*a)
check(t = 2*abs(a))
t = abs(-2*a)
check(t = 2*abs(a))
t = abs(2.0*a)
check(t = 2*abs(a))
t = abs(-2.0*a)
check(t = 2*abs(a))
t = abs(a-b)+abs(b-a)
check(t = 2*abs(a-b))
t = abs(3 + 4 i)
check(t = 5)
t = abs((2,3,4))
check(t = 29^(1/2))
t = abs(a*b)
check(t = abs(a)*abs(b))
t = abs(a/b)
check(t = abs(a)/abs(b))
t = abs(1/a^b)
check(t = 1/(abs(a^b)))

"Testing bug fixes"

clear
check(i exp(1/4 i pi) exp(1/4 i pi) == -1)
-- check(i exp(1/8 i pi) == exp(5/8 i pi))
check(prefixform(cos(1/3.0 pi)) == "0.500000")
check(prefixform(sin(1/6.0 pi)) == "0.500000")

"All tests passed"
</textarea>
</td><td>
<button onclick="run()" style="font-size:20pt">Run</button>
</td></tr></table>

<p>
<div id="stdout"></div>

</body>
</html>
